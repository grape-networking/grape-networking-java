buildscript {
    ext {
        jUnitVersion = "5.7.2"
    }
    repositories {
        google()
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
        maven { url "https://plugins.gradle.org/m2/" }
    }

    dependencies {
        classpath "com.android.tools.build:gradle:7.1.0-alpha01"
        classpath "com.google.protobuf:protobuf-gradle-plugin:0.8.16"
    }
}

apply plugin: 'java-library'
apply plugin: 'com.android.lint'
apply plugin: 'jacoco'
apply plugin: 'maven-publish'
//apply plugin: 'com.google.protobuf'

compileJava   {
    sourceCompatibility = '11'
    targetCompatibility = '11'
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

dependencies {
    // note: this gives a lint warning, but if we take it out, it doesn't find annotations
    // https://stackoverflow.com/a/64726112
    //noinspection AnnotationProcessorOnCompilePath
    implementation 'org.projectlombok:lombok:1.18.20'
    annotationProcessor 'org.projectlombok:lombok:1.18.20'

    implementation "org.slf4j:slf4j-api:1.7.30"
    implementation ('com.papertrailapp:logback-syslog4j:1.0.0') {
        exclude group: 'ch.qos.logback'
    }

    api "io.grpc:grpc-protobuf-lite:1.38.0" // CURRENT_GRPC_VERSION
    implementation 'io.grpc:grpc-stub:1.38.0' // CURRENT_GRPC_VERSION
    testRuntimeOnly "uk.org.lidalia:slf4j-test:1.2.0"
    testImplementation "org.junit.jupiter:junit-jupiter-api:${jUnitVersion}"
    testImplementation "org.junit.jupiter:junit-jupiter-params:${jUnitVersion}"
    testImplementation "org.mockito:mockito-inline:3.10.0"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${jUnitVersion}"
}

lintOptions {
    warningsAsErrors true
    showAll true
    explainIssues true
    xmlOutput file("build/reports/lint/report.xml")
    htmlOutput file("build/reports/lint/report.html")
}

jacoco {
    toolVersion = '0.8.5' // type String
}

test {
    useJUnitPlatform()
}

jacocoTestReport {
    dependsOn test // tests are required to run before generating the report
    reports {
        xml.enabled true
        xml.destination file("${buildDir}/reports/jacoco/jacoco.xml")
        csv.enabled true
        csv.destination file("${buildDir}/reports/jacoco/jacoco.csv")
        html.enabled true
        html.destination file("${buildDir}/reports/jacoco/")
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'network.grape'
            artifactId = 'lib'
            version = '1.0'

            from components.java
        }
    }
}

apply from: '../checkstyle.gradle'

task updatePom(type: Copy) {
    from "$buildDir/publications/maven/pom-default.xml"
    into "."
    rename ("pom-default.xml", "pom.xml")
    dependsOn(generatePomFileForMavenPublication)
}

build.dependsOn(updatePom)

//protobuf {
//    protoc { artifact = 'com.google.protobuf:protoc:3.12.0' }
//    plugins {
//        grpc { artifact = 'io.grpc:protoc-gen-grpc-java:1.37.0' // CURRENT_GRPC_VERSION
//        }
//    }
//    generateProtoTasks {
//        all().each { task ->
//            task.builtins {
//                java { option 'lite' }
//            }
//            task.plugins {
//                grpc { // Options added to --grpc_out
//                    option 'lite' }
//            }
//        }
//    }
//}